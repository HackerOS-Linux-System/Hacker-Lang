WHITESPACE = _{ " " | "\t" }

ident    = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_" | "-")* }
number   = @{ ASCII_DIGIT+ }
str_lit  = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }
version  = @{ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)* }
lib_name = @{ (ASCII_ALPHANUMERIC | "_" | "-" | "/")+ }
lib_type = @{ "source" | "core" | "bytes" | "github" | "virus" | "vira" }
lib_ref  =  { "<" ~ lib_type ~ "/" ~ lib_name ~ (":" ~ version)? ~ ">" }

rest     = @{ (!EOI ~ ANY)+ }
cond     = @{ (!(" > " | " |> " | " <| ") ~ !EOI ~ ANY)+ }
try_body = @{ (!(" catch ") ~ !EOI ~ ANY)+ }
call_path = @{ ("." ~ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_" | "-")*)+ }
cpfx      = { "|>" | "<|" | ">" }

// Reguła line jest wywoływana TYLKO gdy Rust stwierdzi że linia NIE jest przypisaniem
line = { SOI ~ sudo? ~ stmt ~ EOI }
sudo = { "^" }

stmt = {
    class_def   |
    unsafe_def  |
    func_def    |
    func_done   |
    call_stmt   |
    sys_dep     |
    lib_stmt    |
    sep_cmd     |
    raw_cmd     |
    expl_cmd    |
    try_stmt    |
    while_stmt  |
    for_stmt    |
    log_stmt    |
    lock_stmt   |
    unlock_stmt |
    loop_stmt   |
    elif_stmt   |
    else_stmt   |
    if_stmt     |
    bg_stmt     |
    plugin_stmt |
    extern_stmt |
    enum_stmt   |
    struct_stmt |
    import_stmt |
    raw_blk_s   |
    raw_blk_e   |
    end_stmt
}

class_def  = { ";;" ~ ident ~ "def" }
unsafe_def = { "::" ~ ident ~ "def" }
func_def   = { ":"  ~ ident ~ "def" }
func_done  = { "done" }
call_stmt  = { call_path }
sys_dep    = { "//" ~ rest }
lib_stmt   = { "#" ~ lib_ref }
sep_cmd    = { ">>>" ~ rest }
raw_cmd    = { ">>"  ~ rest }
expl_cmd   = { cpfx  ~ rest }
loop_stmt  = { "="   ~ number   ~ cpfx ~ rest }
elif_stmt  = { "??"  ~ cond     ~ cpfx ~ rest }
else_stmt  = { "?:"  ~ cpfx     ~ rest }
if_stmt    = { "?"   ~ cond     ~ cpfx ~ rest }
while_stmt = { "while" ~ cond   ~ cpfx ~ rest }
for_stmt   = { "for" ~ ident ~ "in" ~ cond ~ cpfx ~ rest }
bg_stmt    = { "&"      ~ rest }
log_stmt   = { "log"    ~ str_lit }
lock_stmt  = { "lock"   ~ "$" ~ ident ~ "=" ~ rest }
unlock_stmt = { "unlock" ~ "$" ~ ident }

// \\ plugin_name [args] — ~/.hackeros/hacker-lang/plugins/<name> lub <name>.hl
plugin_stmt = { "\\\\" ~ rest }

// -- [static] path — linkowanie zewnętrzne
extern_stmt = { "--" ~ rest }

enum_stmt   = { "==" ~ ident ~ "[" ~ (ident ~ ("," ~ ident)*)? ~ "]" }
struct_stmt = { "struct" ~ ident ~ "[" ~ (sf ~ ("," ~ sf)*)? ~ "]" }
sf          = { ident ~ ":" ~ ident }
import_stmt = { "<<" ~ str_lit }
try_stmt    = { "try" ~ try_body ~ "catch" ~ rest }
raw_blk_s   = { "[" }
raw_blk_e   = { "]" }
end_stmt    = { "end" ~ number? }
